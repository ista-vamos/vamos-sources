start: events_and_imports statements
events_and_imports: (eventsspec [imports] | imports eventsspec)
// for inlined specifications
inlined: imports statements

eventsspec: [eventsfile] eventdecl+
eventsfile: "events" ESCAPED_STRING ";"? WSEOL

imports: importmod+
importmod: "import" name ";"? WSEOL
WSEOL: /\s*\n/

statements: statement+

statement: _statement_expr ";"? | _statement ";"
_statement_expr: let | foreach | ifexpr | newexpr
_statement: runcommand | methodcall | yieldto | cont | brk

cont: "continue"
brk: "break"

let: "let" name "=" expr
ifexpr: "if" (boolexpr | methodcall | value) "{" statements "}" ["else" "{" statements "}"]
foreach: "foreach" name "in" iterable "{" statements "}"
runcommand: "run" /[^\n]/*
yieldto: "yield" eventseq "to" tracename
eventseq: event+
tracename: NAME
event: name ["(" params ")"]

expr: newexpr | boolexpr | methodcall | specarg | constant
specarg: "$" NUMBER
iterable: name | methodcall

newexpr: "new" type
methodcall: name "." name "(" [params] ")"
params: (name | expr) ("," (name | expr))*
value: NAME

//%import types.typeannot
%import types.type
%import expr.boolexpr
%import expr.constant
%import events.eventdecl_imp -> eventdecl
%import comm.name

%import common.CNAME -> NAME
%import common.NUMBER
%import common.ESCAPED_STRING
%import common.WS_INLINE
%import common.WS

COMMENT: "--" /[^\n]/*

%ignore COMMENT
%ignore WS